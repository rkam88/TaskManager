apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    signingConfigs {
        release {
            Properties localProperties = new Properties()
            localProperties.load(project.rootProject.file("local.properties").newDataInputStream())
            storeFile file(localProperties.getProperty("storeFile"))
            storePassword localProperties.getProperty("storePassword")
            keyAlias = localProperties.getProperty("keyAlias")
            keyPassword localProperties.getProperty("keyPassword")
        }
    }

    def majorVersion = 0
    def minorVersion = 1
    def patchVersion = 0

    defaultConfig {
        applicationId "com.langfordapps.taskmanager"
        minSdkVersion 23
        targetSdkVersion 29
        versionCode majorVersion * 10000 + minorVersion * 100 + patchVersion * 1
        versionName "$majorVersion.$minorVersion.$patchVersion"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        signingConfig signingConfigs.release

        archivesBaseName = "task-manager-v${versionName}"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
        freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'

    // RecyclerView
    implementation 'androidx.recyclerview:recyclerview:1.2.0'

    // Kotlin extensions
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.3.1"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1"
    implementation 'androidx.core:core-ktx:1.3.2'

    // Material
    implementation 'com.google.android.material:material:1.3.0'

    // Dagger
    implementation 'com.google.dagger:dagger:2.28.3'
    kapt 'com.google.dagger:dagger-compiler:2.26'

    // Room
    def room_version = "2.3.0"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    testImplementation "androidx.room:room-testing:$room_version"

    // Testing
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.3.7'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-inline:2.8.47'
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0"
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}
